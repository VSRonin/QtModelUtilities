cmake_minimum_required(VERSION 3.5)
project(QtModelUtilities LANGUAGES CXX)
set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake/modules ${CMAKE_MODULE_PATH})
set(CMAKE_INCLUDE_CURRENT_DIR ON)

option(BUILD_STATIC_LIBS "Build the static library" OFF)
option(BUILD_EXAMPLES "Build the examples" OFF)
option(NO_GUI "Disable all features requiring QtGui and QtWidgets" OFF)
option(NO_WIDGETS "Disable all features requiring QtWidgets" OFF)
option(BUILD_DOCS "Enables or disables the build of the documentation" OFF)
option(BUILD_ROLEMASKPROXY "Enables or disables the build of Role Mask Proxy Model" ON)
option(BUILD_MODELSERIALISATION "Enables or disables the build of Model Serialisation" ON)
option(BUILD_INSERTPROXY "Enables or disables the build of Insert Proxy Model" ON)
option(OPTIMISE_FOR_MANY_ROLES "Set this property to ON if you plan to store more than 20 different roles in the models to optimise performance" OFF)

include(CTest)
include(GetGitRevisionDescription)
git_describe(GitTagVersion --tags)
string(REGEX REPLACE "^v([0-9]+)\\..*" "\\1" VERSION_MAJOR "${GitTagVersion}")
string(REGEX REPLACE "^v[0-9]+\\.([0-9]+).*" "\\1" VERSION_MINOR "${GitTagVersion}")
string(REGEX REPLACE "^v[0-9]+\\.[0-9]+\\.([0-9]+).*" "\\1" VERSION_PATCH "${GitTagVersion}")
set(VERSION_SHORT "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}")

if("${CMAKE_SIZEOF_VOID_P}" STREQUAL "4")
    set(modelutilities_IS64BITS OFF)
else()
    set(modelutilities_IS64BITS ON)
endif()
if(modelutilities_IS64BITS)
    set(modelutilities_PlatformDir "x64")
else()
    set(modelutilities_PlatformDir "x86")
endif()

if(CMAKE_BUILD_TYPE MATCHES DEBUG)
    set(BUILD_DOCS OFF)
endif()
add_subdirectory(src)

if(BUILD_EXAMPLES)
    add_subdirectory(examples)
endif()
if(BUILD_TESTING)
    enable_testing()
    option(TEST_OUTPUT_XML "Redirects the test output to xml files in the TestResults folder" OFF)
    if(TEST_OUTPUT_XML)
        file(MAKE_DIRECTORY "${CMAKE_BINARY_DIR}/TestResults")
    endif()
    add_subdirectory(tests)
endif()

